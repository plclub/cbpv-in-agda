import Relation.Binary.PropositionalEquality as Eq
open import Axiom.Extensionality.Propositional using (Extensionality)
open import Data.Fin using (Fin; suc; zero)
open import Data.Nat using (â„•; suc)
open import Level using (0â„“)
open Eq using (_â‰¡_; refl; sym)

open import CBV.Effects.Terms
open import CBPV.Effects.Terms hiding (n)
open import Effects

module CBV.Effects.Translation (E : Effect) where

import CBPV.Effects.SyntacticTyping E as CBPV
open import CBPV.Effects.Renaming E
open import CBPV.Effects.Types E
open import CBV.Effects.SyntacticTyping E
open import CBV.Effects.Types E
open CBPV hiding (Ctx; _âˆ·_; Î“)
open Effect E
open Effects.Properties E

postulate
  extensionality : Extensionality 0â„“ 0â„“

record Translation (A B : Set) : Set where
  field
    âŸ¦_âŸ§ : A â†’ B

open Translation â¦ƒ...â¦„ public

instance
  âŸ¦TypeâŸ§ : Translation Type ValType
  Translation.âŸ¦ âŸ¦TypeâŸ§ âŸ§ ğŸ™ = ğŸ™
  Translation.âŸ¦ âŸ¦TypeâŸ§ âŸ§ (Ï„â‚ â”€ Ï† âŸ¶ Ï„â‚‚) = ğ‘¼ Ï† (âŸ¦ Ï„â‚ âŸ§ â‡’ ğ‘­ âŸ¦ Ï„â‚‚ âŸ§)
  Translation.âŸ¦ âŸ¦TypeâŸ§ âŸ§ (Ï„â‚ * Ï„â‚‚) = âŸ¦ Ï„â‚ âŸ§ * âŸ¦ Ï„â‚‚ âŸ§
  Translation.âŸ¦ âŸ¦TypeâŸ§ âŸ§ (Ï„â‚ âˆª Ï„â‚‚) = âŸ¦ Ï„â‚ âŸ§ âˆª âŸ¦ Ï„â‚‚ âŸ§

  âŸ¦CtxâŸ§ : Translation (Ctx n) (CBPV.Ctx n)
  Translation.âŸ¦ âŸ¦CtxâŸ§ âŸ§ Î“ m = âŸ¦ Î“ m âŸ§

  mutual
    âŸ¦ValueâŸ§ : Translation (Value n) (Val n)
    Translation.âŸ¦ âŸ¦ValueâŸ§ âŸ§ unit = unit
    Translation.âŸ¦ âŸ¦ValueâŸ§ âŸ§ (Æ› e) = âŸª Æ› âŸ¦ e âŸ§ âŸ«
    Translation.âŸ¦ âŸ¦ValueâŸ§ âŸ§ (â™¯ x) = â™¯ x
    Translation.âŸ¦ âŸ¦ValueâŸ§ âŸ§ (inl v) = inl âŸ¦ v âŸ§
    Translation.âŸ¦ âŸ¦ValueâŸ§ âŸ§ (inr v) = inr âŸ¦ v âŸ§
    Translation.âŸ¦ âŸ¦ValueâŸ§ âŸ§ âŸ¨ vâ‚ , vâ‚‚ âŸ© = âŸ¨ âŸ¦ vâ‚ âŸ§ , âŸ¦ vâ‚‚ âŸ§ âŸ©

    âŸ¦ExpâŸ§ : Translation (Exp n) (Comp n)
    Translation.âŸ¦ âŸ¦ExpâŸ§ âŸ§ (val v) = return âŸ¦ v âŸ§
    Translation.âŸ¦ âŸ¦ExpâŸ§ âŸ§ (eâ‚ Â· eâ‚‚) =
      $âŸµ âŸ¦ eâ‚ âŸ§ â‹¯
      $âŸµ âŸ¦ eâ‚‚ âŸ§ [ suc ]c â‹¯
      (â™¯ suc zero !) Â· â™¯ zero
    Translation.âŸ¦ âŸ¦ExpâŸ§ âŸ§ (eâ‚ Â» eâ‚‚) =
      $âŸµ âŸ¦ eâ‚ âŸ§ â‹¯
      â™¯ zero Â» âŸ¦ eâ‚‚ âŸ§ [ suc ]c
    Translation.âŸ¦ âŸ¦ExpâŸ§ âŸ§ (inl e) =
     $âŸµ âŸ¦ e âŸ§ â‹¯ return (inl (â™¯ zero))
    Translation.âŸ¦ âŸ¦ExpâŸ§ âŸ§ (inr e) =
     $âŸµ âŸ¦ e âŸ§ â‹¯ return (inr (â™¯ zero))
    Translation.âŸ¦ âŸ¦ExpâŸ§ âŸ§ âŸ¨ eâ‚ , eâ‚‚ âŸ© =
      $âŸµ âŸ¦ eâ‚ âŸ§ â‹¯
      $âŸµ âŸ¦ eâ‚‚ âŸ§ [ suc ]c â‹¯
      return âŸ¨ â™¯ suc zero , â™¯ zero âŸ©
    Translation.âŸ¦ âŸ¦ExpâŸ§ âŸ§ (case e inlâ‡’ eâ‚ inrâ‡’ eâ‚‚) =
      $âŸµ âŸ¦ e âŸ§ â‹¯
      case â™¯ zero
        inlâ‡’ âŸ¦ eâ‚ âŸ§ [ â†‘â†‘ ]c
        inrâ‡’ âŸ¦ eâ‚‚ âŸ§ [ â†‘â†‘ ]c
    Translation.âŸ¦ âŸ¦ExpâŸ§ âŸ§ ($â‰” eâ‚ â‹¯ eâ‚‚) =
      $âŸµ âŸ¦ eâ‚ âŸ§ â‹¯
      $â‰” â™¯ zero â‹¯
      âŸ¦ eâ‚‚ âŸ§ [ â†‘â†‘â†‘ ]c
    Translation.âŸ¦ âŸ¦ExpâŸ§ âŸ§ tick = tick

âŸ¦Î“âˆ·Ï„âŸ§-expand : âŸ¦ Î“ âˆ· Ï„ âŸ§ â‰¡ âŸ¦ Î“ âŸ§ CBPV.âˆ· âŸ¦ Ï„ âŸ§
âŸ¦Î“âˆ·Ï„âŸ§-expand = extensionality Î» where
                                  zero    â†’ refl
                                  (suc m) â†’ refl

mutual
  translation-preservation-value : Î“ âŠ© v â¦‚ Ï„ # Ï†
                                   ----------------------
                                 â†’ âŸ¦ Î“ âŸ§ âŠ¢v âŸ¦ v âŸ§ â¦‚ âŸ¦ Ï„ âŸ§
  translation-preservation-value typeUnit = typeUnit
  translation-preservation-value typeVar = typeVar
  translation-preservation-value {Î“ = Î“} (typeAbs {Ï„ = Ï„} âŠ¢eâ€²)
    with translation-preservation-exp âŠ¢eâ€²
  ...  | âŠ¢âŸ¦eâŸ§
    rewrite âŸ¦Î“âˆ·Ï„âŸ§-expand {Î“ = Î“} {Ï„} = typeThunk (typeAbs âŠ¢âŸ¦eâŸ§)
  translation-preservation-value (typePair âŠ©vâ‚ âŠ©vâ‚‚) =
    typePair
      (translation-preservation-value âŠ©vâ‚)
      (translation-preservation-value âŠ©vâ‚‚)
  translation-preservation-value (typeInl âŠ©v) =
    typeInl (translation-preservation-value âŠ©v)
  translation-preservation-value (typeInr âŠ©v) =
    typeInr (translation-preservation-value âŠ©v)

  translation-preservation-exp : Î“ âŠ¢ e â¦‚ Ï„ # Ï†
                                 ----------------------------
                               â†’ âŸ¦ Î“ âŸ§ âŠ¢c âŸ¦ e âŸ§ â¦‚ ğ‘­ âŸ¦ Ï„ âŸ§ # Ï†
  translation-preservation-exp (typeVal âŠ©v) =
    typeRet (translation-preservation-value âŠ©v)
  translation-preservation-exp (typeApp âŠ¢eâ‚ âŠ¢eâ‚‚ Ï†â‚+Ï†â‚‚+Ï†â‚ƒâ‰¤Ï†) =
    typeLetin
      (translation-preservation-exp âŠ¢eâ‚)
      (typeLetin
        (comp-typepres-renaming
          (translation-preservation-exp âŠ¢eâ‚‚)
          Î» _ â†’ refl)
        (typeApp (typeForce typeVar â‰¤-refl) typeVar)
        â‰¤-refl)
      (â‰¤-trans (â‰¡â†’â‰¤ (sym +-assoc)) Ï†â‚+Ï†â‚‚+Ï†â‚ƒâ‰¤Ï†)
  translation-preservation-exp (typeSeq âŠ¢eâ‚ âŠ¢eâ‚‚ Ï†â‚+Ï†â‚‚â‰¤Ï†) =
    typeLetin
      (translation-preservation-exp âŠ¢eâ‚)
      (typeSequence
        typeVar
        (comp-typepres-renaming
          (translation-preservation-exp âŠ¢eâ‚‚)
          Î» m â†’ refl))
      Ï†â‚+Ï†â‚‚â‰¤Ï†
  translation-preservation-exp (typePair âŠ¢eâ‚ âŠ¢eâ‚‚ Ï†â‚+Ï†â‚‚â‰¤Ï†) =
    typeLetin
      (translation-preservation-exp âŠ¢eâ‚)
      (typeLetin
        (comp-typepres-renaming
          (translation-preservation-exp âŠ¢eâ‚‚)
          (Î» _ â†’ refl))
        (typeRet (typePair typeVar typeVar))
        (â‰¡â†’â‰¤ +-pure-idÊ³))
      Ï†â‚+Ï†â‚‚â‰¤Ï†
  translation-preservation-exp (typeInl âŠ¢e) =
    typeLetin
      (translation-preservation-exp âŠ¢e)
      (typeRet (typeInl typeVar))
      (â‰¡â†’â‰¤ +-pure-idÊ³)
  translation-preservation-exp (typeInr âŠ¢e) =
    typeLetin
      (translation-preservation-exp âŠ¢e)
      (typeRet (typeInr typeVar))
      (â‰¡â†’â‰¤ +-pure-idÊ³)
  translation-preservation-exp (typeCase âŠ¢e âŠ¢eâ‚ âŠ¢eâ‚‚ Ï†â‚+Ï†â‚‚â‰¤Ï†) =
    typeLetin
      (translation-preservation-exp âŠ¢e)
      (typeCase
        typeVar
        (comp-typepres-renaming
          (translation-preservation-exp âŠ¢eâ‚)
          Î» where zero â†’ refl ; (suc _) â†’ refl)
        (comp-typepres-renaming
          (translation-preservation-exp âŠ¢eâ‚‚)
          Î» where zero â†’ refl ; (suc _) â†’ refl))
       Ï†â‚+Ï†â‚‚â‰¤Ï†
  translation-preservation-exp (typeSplit âŠ¢eâ‚ âŠ¢eâ‚‚ Ï†â‚+Ï†â‚‚â‰¤Ï†) =
    typeLetin
      (translation-preservation-exp âŠ¢eâ‚)
      (typeSplit
        typeVar
        (comp-typepres-renaming
          (translation-preservation-exp âŠ¢eâ‚‚)
          Î» where zero â†’ refl ; (suc zero) â†’ refl ; (suc (suc _)) â†’ refl))
      Ï†â‚+Ï†â‚‚â‰¤Ï†
  translation-preservation-exp (typeTick tockâ‰¤Ï†) =
    typeTick tockâ‰¤Ï†
